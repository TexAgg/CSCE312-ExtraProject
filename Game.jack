class Game {

    //Define the variables
    field Snake snake;
    field int dx;
    field int dy;
    field int score;
    field int level;
    field int delay;
    field Grid grid;

    constructor Game new() {
        //Initialize the game variables
        let score = 0;
        let level = 1;
        let delay = 100;
        let grid = Grid.new(this, 512, 256);

        //CREATE SNAKE HERE
        let snake = Snake.new();
        let dx = 0;
        let dy = 1;

        do grid.displayFood();
        return this;
    }

    method void dispose() {
        //DO SNAKE DISPOSE HERE
        do snake.dispose();
        do grid.dispose();
        do Memory.deAlloc(this);
        return;
    }

    // Main game loop.
    method void run(){
        var int key;
        var boolean close;
        var Point foodLoc, snekLoc;
        var int dist;

        let close = false;

        while (~close) {

            let key = Keyboard.keyPressed();

            if (key = 81) {
                let close = true;
            } 
            if (key = 130 & ~(dx = 1)) {
                //left arrow --> set snake directions
                let dx = -1;
                let dy = 0;
            }
            if (key = 131 & ~(dy = 1)) {
                //up arrow --> set snake direction
                let dx = 0;
                let dy = -1;
            }
            if (key = 132 & ~(dx = -1)) {
                //right arrow --> set snake directions
                let dx = 1;
                let dy = 0;
            }
            if (key = 133 & ~(dy = -1)) {
                //down arrow --> set snake direction
                let dx = 0;
                let dy = 1;
            }

            // If the snake runs into food.
            let foodLoc = grid.getFoodLocation();
            let snekLoc = snake.getHead();
            if (~(Math.abs(foodLoc.getX()-snekLoc.getX()) > 5) & ~(Math.abs(foodLoc.getY() - snekLoc.getY()) > 5)) {
                do snake.eatFood();
                do grid.createFood();
                do Screen.setColor(false);
                do Screen.drawRectangle(foodLoc.getX(), foodLoc.getY(), foodLoc.getX()+5, foodLoc.getY()+5);

                do Output.printInt(snake.getNumEaten());
            }

            // If the snake eats enough food
            if (~(snake.getNumEaten() < 15)) {
                do Output.printString("You win!");
                do Sys.halt();
            }
            
            do Sys.wait(100);
            do snake.move(dx, dy);

        }
        do Sys.halt();
        return;

    }
}
