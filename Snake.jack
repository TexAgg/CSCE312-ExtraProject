// Chapter 12

class Snake {
	
	// Number of blocks in the snake.
	field int length;
	/* An array of Points.
	Container of all the blocks in the snake. */
	field Array body;
	// How much food has been eaten.
	field int foodEaten;
	// This is the length of each block.
	field int l;

	constructor Snake new() {
		let l = 5;
		// The snake can't be longer than 30 pixels.
		let body = Array.new(30);
		let body[0] = Point.new(0, 4*l);
		let body[1] = Point.new(0, 3*l);
		let body[2] = Point.new(0, 2*l);
		let body[3] = Point.new(0, l);
		let body[4] = Point.new(0, 0);
		// Initial length is 5.
		let length = 5;

		//let p = body[0];
		do draw();

		return this;
	}

	// Like c++ delete I guess.
	method void dispose() {
		do body.dispose();
		do Memory.deAlloc(this);
		return;
	}

	/*
	Draws the snake.
	I guess just call this every few milliseconds or something.
	*/
	method void draw() {
		var int i;
		var Point p;

		//do printBody();
		
		do Screen.clearScreen();
		do Screen.setColor(true);

		// Loop through body and color in the pixels.
		let i = 0;
		while (i < length) {
			let p = body[i];
			let p = wrap(p);
			let body[i] = p;
			do Screen.drawRectangle(p.getX(), p.getY(), p.getX() + l, p.getY() + l);

			let i = i + 1;
		}

		return;
	}

	/* 
	Move the snake by taking the last block and moving it to the front.
	ddx or ddy should either be 1, -1, or 0. 
	Also, exactly one of them must be 0, and the other must be nonzero.
	*/
	method void move(int ddx, int ddy) {
		var Point p, last;
		var int i;

		let p = body[0];

		let i = length-1;
		while (i > 0) {
			let body[i] = body[i-1];

			let i = i - 1;
		}

		// JACK DOES NOT RESPECT ORDER OF OPERATIONS
		// PAGE 188
		let body[0] = Point.new(p.getX() + (ddx*l), p.getY() + (ddy*l));

		do draw();

		return;
	}

	// Add a block to the end.
	method void grow() {
		var Point p, last;
		
		let last = body[length - 1];
		let p = Point.new(last.getX()-l, last.getY()-l);
		let p = wrap(p);
		let body[length] = p;
		let length = length + 1;

		do draw();

		return;
	}

	// https://bitbucket.org/gaikema/snake/src/c2de2507858e2c67f0952f37590978b3152ccf64/Snake.ts?at=master&fileviewer=file-view-default#Snake.ts-67
	method Point wrap(Point p) {
		var int width, height;
		let width = 511;
		let height = 255;
		
		if (p.getX() < 0) {
			do p.setX(width);
			return p;
		}
		if (~(p.getX()+l < width)) {
			do p.setX(0);
			return p;
		}

		if (p.getY() < 0) {
			do p.setY(height);
			return p;
		}
		if (~(p.getY()+l < height)) {
			do p.setY(0);
			return p;
		}
		
		return p;
	}

	method int getLength() {
		return length;
	}

	method void printBody() {
		var int i;
		var Point p;

		let i = 0;
		while (i < length) {
			let p = body[i];
			do Output.printInt(i);
			do p.print();
			do Sys.wait(1000);

			let i = i+1;
		}

		return;
	}
}